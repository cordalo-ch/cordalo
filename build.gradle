buildscript {
    ext {
        corda_release_group = 'net.corda'
        corda_release_group_plugins = 'net.corda.plugins'
        corda_release_version = '4.1'
        corda_gradle_plugins_version = '4.0.42'
        junit_version = '4.12'
        quasar_version = '0.7.10'
        spring_boot_version = '2.0.9.RELEASE'
        spring_boot_gradle_plugin_version = '2.0.9.RELEASE'
        slf4j_version = '1.7.25'
        log4j_version = '2.11.2'
        corda_platform_version = '4'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
    }

    dependencies {
        classpath "$corda_release_group_plugins:cordapp:$corda_gradle_plugins_version"
        classpath "$corda_release_group_plugins:cordformation:$corda_gradle_plugins_version"
        classpath "$corda_release_group_plugins:quasar-utils:$corda_gradle_plugins_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_gradle_plugin_version"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
    }
}
allprojects {
    apply plugin: 'java'
    apply plugin: 'signing'
    apply plugin: 'com.bmuschko.nexus'

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda' }
        maven { url 'https://jitpack.io' }
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters" // Required by Corda's serialisation framework.
    }

    jar {
        // This makes the JAR's SHA-256 hash repeatable.
        preserveFileTimestamps = false
        reproducibleFileOrder = true
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    modifyPom {
        project {
            name 'cordalo.ch CORDA extensions'
            description 'cordalo.ch is a simple framework for fast CORDA development.'
            url 'https://github.com/cordalo-ch/cordalo'
            inceptionYear '2019'

            scm {
                url 'https://github.com/cordalo-ch/cordalo'
                connection 'scm:https://github.com/cordalo-ch/cordalo.git'
                developerConnection 'scm:git://github.com/cordalo-ch/cordalo.git'
            }

            licenses {
                license {
                    name 'MIT'
                    url 'https://cordalo.ch/LICENCE.md'
                }
            }

            developers {
                developer {
                    id 'lolo8304'
                    name 'Lorenz HÃ¤nggi aka Lolo'
                    email 'lolo8304@gmail.com'
                }
            }
        }


        extraArchive {
            sources = true
            tests = true
            javadoc = true
        }

        nexus {
            sign = true
            repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }

        signing {
            sign configurations.archives
        }
    }

    extraArchive {
        sources = false
        tests = true
        javadoc = false
    }

    nexus {
        sign = true
        repositoryUrl = 'http://localhost:8081/nexus/content/repositories/internal/'
        snapshotRepositoryUrl = 'http://localhost:8081/nexus/content/repositories/internal-snapshots/'
    }
}

ext {
    cordalo_revision = "git rev-parse HEAD".execute().text.trim()
}

apply plugin: 'net.corda.plugins.quasar-utils'


sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'ch.cordalo'
version '0.1-SNAPSHOT'

dependencies {
    compile "$corda_release_group:corda-core:$corda_release_version"
    compile "$corda_release_group:corda-jackson:$corda_release_version"
    compile "$corda_release_group:corda-test-utils:$corda_release_version"
    compile "$corda_release_group:corda-node-driver:$corda_release_version"
    testCompile "junit:junit:$junit_version"
}


task installQuasar(type: Copy) {
    destinationDir rootProject.file("lib")
    from(configurations.quasar) {
        rename 'quasar-core(.*).jar', 'quasar.jar'
    }
}

jar {
    // Prevent the root project from building an unwanted dummy CorDapp.
    enabled = true
}

// based on docu https://central.sonatype.org/pages/working-with-pgp-signatures.html
// https://central.sonatype.org/pages/gradle.html
